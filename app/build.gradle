import java.text.DateFormat
import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.mob.sdk'
apply plugin: 'com.zq.sdk'


kotlin {
    experimental {
        coroutines 'enable'
    }
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId "com.zq.zqplayer"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            //选择要添加的对应 cpu 类型的 .so 库。
            abiFilters 'x86','armeabi-v7a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64','armeabi', 'armeabi-v7a', 'arm64-v8a'
        }

        manifestPlaceholders = [
                JPUSH_PKGNAME : applicationId,
                JPUSH_APPKEY : "d2d50490d82cbadcae4cbfdc", //JPush 上注册的包名对应的 Appkey.
                JPUSH_CHANNEL : "main", //暂时填写默认值即可. <!-- JPUSH_CHANNEL 是为了方便开发者统计 APK 分发渠道。-->
        ]

    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    signingConfigs {
        config {
            keyAlias 'zaiqiang'
            keyPassword 'zaiqiang'
            storeFile file('../signature.jks')
            storePassword 'zaiqiang'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = outputFile.name.replace(outputFile.name, "zqplayer_${getGitBranch()}_${variant.buildType.name}_${releaseTime()}.apk")
                outputFileName = fileName
            }
        }
    }
}

// 打包时间
def releaseTime() {
    DateFormat df = new SimpleDateFormat("yyyyMMddHHmm")
    Calendar calendar = Calendar.getInstance()
    String dateName = df.format(calendar.getTime())
    return dateName
}

def getGitBranch() {
    return 'git symbolic-ref --short -q HEAD'.execute().text.trim()
}

dependencies {

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha03'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    api project(":playerlib")
    //ZQSDK 会添加以下依赖
//    api 'com.ziq.base:base:+'
//    api 'com.zq.customviewlib:customviewlib:+'

    kapt(rootProject.ext.dependencies["butterknife-compiler"]) {
        exclude module: 'support-annotations'
    }
    kapt (rootProject.ext.dependencies["dagger2-compiler"]){
        exclude module: 'support-annotations'
    }

    api rootProject.ext.dependencies["appcompat"]
    api rootProject.ext.dependencies["recyclerview"]
    api rootProject.ext.dependencies["design"]

    api 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'
    //FlycoTabLayout第三方tab https://github.com/H07000223/FlycoTabLayout/blob/master/README_CN.md
    api rootProject.ext.dependencies["flycoTabLayout_Lib"]

    //极光推送
    api 'cn.jiguang.sdk:jpush:3.1.7'  // 此处以JPush 3.1.6 版本为例。
    api 'cn.jiguang.sdk:jcore:1.2.6'  // 此处以JCore 1.2.5 版本为例。
    api 'com.orhanobut:logger:2.2.0'
    api 'me.leolin:ShortcutBadger:1.1.22@aar'

    // Architecture Components
    implementation rootProject.ext.dependencies["lifecycle-extensions"]
    annotationProcessor rootProject.ext.dependencies["lifecycle-compiler"]

    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.27.0-eap13'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.27.0-eap13'

}

ZQSDK {
    //依赖 库
    //添加了基础库依赖，默认是添加最新版 “+”
    BaseLib{
//            versionName '3.1.2'
    }
    CustomViewLib{}

    //会移除 基础库依赖
//    configurations.all {
//        exclude group: 'com.ziq.base', module: 'base'
//    }
}


MobSDK {
    appKey "28fdc29fdfb87"
    appSecret "a4b98f7a64b94d21d4be3a51d3ea987d"

    ShareSDK {
        //平台配置信息
        devInfo {
            SinaWeibo {
                appKey "2594507670"
                appSecret "880f6e17566da98f5e301d4e6785d105"
                shareByAppClient true
            }
//            Wechat {
//                appId "wx746d4aca852fd114"
//                appSecret "0f4399c55ce47151d3840929c94d1cd1"
//                shareByAppClient true
//                bypassApproval false //bypassApproval表示是否绕过审核  https://blog.csdn.net/gf771115/article/details/44858007
//            }
//            QQ {
//                appId "1106565528"
//                appKey "X6CnxgtbqxEDetTg"
//                shareByAppClient true
//            }
        }
    }

}